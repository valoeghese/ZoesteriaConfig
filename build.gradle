plugins {
	id 'maven-publish'
}

allprojects {
	apply plugin: 'java'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

archivesBaseName = project.archives_base_name
version = project.version
group = project.maven_group

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy jar
			}
			artifact(sourcesJar) {
				builtBy sourcesJar
			}
		}
    }

	// select the repositories you want to publish to
	repositories {
		if (project.hasProperty('maven_password')) {
			maven {
				url = "http://deploy.modmuss50.me/"
				credentials {
					username = "valoeghese"
					password = project.getProperty('maven_password')
				}
			}
        }
	}
}
